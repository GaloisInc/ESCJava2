(* This program produces one BG_PUSH of the conjunction of BG_PUSH's in
   the input. *)
(* Caveat:  If the input contains no BG_PUSH's, the output may not be a
   valid S-expression. *)
(* Caveat:  There is no special handling for characters, in particular
   semi-colons, inside vertical-bar brackets. *)

PROC Main() IS
  Wr("(BG_PUSH (AND\n");
  DO
    Rd("("); ReadBlanks(); Rd("BG_PUSH"); CopyBlank();
    CopyUntilBeforeCloseParen(); Wr('\n')
  | ReadChar()
  OD;
  Wr("))\n")
END;

PROC ReadBlanks() IS
  DO Rd('\s') | Rd('\n') | ReadComment() OD
END;

PROC ReadChar() IS
  ReadComment() | EVAL Rd()
END;

PROC CopyChar() IS
  ReadComment(); Wr('\n') | Wr(Rd())
END;

PROC CopyBlank() IS
  ReadComment(); Wr('\n') | At('\s'); Wr(Rd()) | At('\n'); Wr(Rd())
END;

PROC ReadComment() IS
  Rd(';'); ReadUntilEoln()
END;

PROC ReadUntilEoln() IS
  TIL Eof()
  |   Rd('\n')
  DO  EVAL Rd()
  END
END;

PROC CopyUntilBeforeCloseParen() IS
  TIL Rd(')')
  DO  Rd('('); Wr('('); CopyUntilBeforeCloseParen(); Wr(')')
  |   CopyChar()
  END
END;
